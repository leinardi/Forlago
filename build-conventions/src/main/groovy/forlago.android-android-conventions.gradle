/*
 * Copyright 2021 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'forlago.detekt-conventions'
}

kapt {
    useBuildCache = true
    correctErrorTypes true
    javacOptions {
        option('-Xmaxerrs', Integer.MAX_VALUE)
    }
}

android {
    compileSdk config.android.compileSdk
    buildToolsVersion config.android.buildTools

    defaultConfig {
        minSdk config.android.minSdk
        targetSdk config.android.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField("String", "DEEP_LINK_SCHEMA", "\"${config.app.deepLinkSchema}\"")
    }
    compileOptions {
        sourceCompatibility config.android.javaVersion
        targetCompatibility config.android.javaVersion
    }
    kotlinOptions {
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-Xsuppress-version-warnings' // Needed to suppress warning due to languageVersion = '1.6' on kotlin 1.5.x
        jvmTarget = config.android.javaVersion
        useIR = true
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }
    lintOptions {
        abortOnError true
        checkAllWarnings false
        checkDependencies true
        checkReleaseBuilds false
        ignoreTestSources true
        warningsAsErrors false
        disable 'ResourceType'
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
    }
    packagingOptions {
        resources {
            exclude '.readme'
            exclude 'LICENSE.txt'
            exclude 'fabric/*.properties'
            // Exclude the Firebase/Fabric/other random properties files
            exclude '/*.properties'
            // Exclude AndroidX version files
            exclude 'META-INF/*.version'
            // Exclude consumer proguard files
            exclude 'META-INF/proguard/*'
            exclude 'META-INF/*.properties'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
            exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
            exclude 'META-INF/*.kotlin_module'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

kotlin {
    sourceSets.all {
        languageSettings {
            languageVersion = '1.6' // Needed to have exhaustive when with kotlin 1.5.x
            progressiveMode = true
        }
    }
}

tasks.withType(Test) {
    testLogging {
        events "skipped", "failed"
    }
}

dependencies {
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestUtil libs.androidx.test.orchestrator
}
